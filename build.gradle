// use the NodeJS plugin for build
plugins {
    id 'com.moowork.node' version '1.2.0'
}

// base plugin for tasks like clean
apply plugin: 'base'

/**
 * Checks if we have any missing connection, meaning we have a connection id that can not be matched with the component id.
 */
task checkConnections(type: NodeTask) {
    description = 'Check missing connections in the model.'
    group = 'Build'

    inputs.dir project.file("${project.rootDir}/model")

    script = file('src/check-connections.js')
}
checkConnections.dependsOn(npmInstall)
assemble.dependsOn(checkConnections)

/**
 * Checks if we have any missing license links.
 */
task checkLicenses(type: NodeTask) {
    description = 'Check missing license links in the model.'
    group = 'Build'

    inputs.dir project.file("${project.rootDir}/model")

    script = file('src/check-licenses.js')
}
checkLicenses.dependsOn(npmInstall)
assemble.dependsOn(checkLicenses)

/**
 * Outputs the model as json.
 */
task modelOutput(type: NodeTask) {
    description = 'Merge all YAML model files into one.'
    group = 'Build'

    inputs.dir project.file("${project.rootDir}/model")
    outputs.file project.file("${project.buildDir}/output/model.json")

    script = file('src/model-output.js')
}
modelOutput.dependsOn(npmInstall)
assemble.dependsOn(modelOutput)